(* C99 ebnf list *)
(* opt = [] repeat = {} *)
(* HEAD *)
translation-unit
        = { external-declaration };

external-declaration
        = function-definition
        | declaration;

function-definition
        = declaration-specifiers, declarator, { declaration }, compound-statement;

(* STATEMENTS *)
statement
        = labeled-statement
        | compound-statement
        | expression-statement
        | selection-statement
        | iteration-statement
        | jump-statement;

labeled-statement
        = identifier, ":", statement
        | "case", constant-expression, ":", statement
        | "default", ":", statement;

compound-statement
        = "{", { block-item }, "}";

block-item
        = declaration
        | statement;

expression-statement
        = [ expression ], ";";

selection-statement
        = "if", "(", expression, ")", statement
        | "if", "(", expression, ")", statement, "else", statement
        | "switch", "(", expression, ")", statement;

iteration-statement
        = "while", "(", expression, ")", statement
        | "do", statement, "while", "(", expression, ")", ";"
        | "for", "(", [ expression ], ";", [ expression ], ";", [ expression ], ")", statement
        | "for", "(", declaration, [ expression ], ";", [ expression ], ")", statement;

jump-statement
        = "goto", identifier, ";"
        | "continue", ";"
        | "break", ";"
        | "return", [ expression ], ";";

(* DECLARATIONS *)
declaration
        = declaration-specifiers, [ init-declarator, [ ",", init-declarator ] ], ";";

declaration-specifiers
        = storage-class-specifier, [ declaration-specifiers ]
        | type-specifier, [ declaration-specifiers ]
        | type-qualifier, [ declaration-specifiers ]
        | function-specifier, [ declaration-specifiers ];

init-declarator
        = declarator
        | declarator, "=", initializer;

storage-class-specifier
        = "typedef"
        | "extern"
        | "static"
        | "auto"
        | "register";

type-specifier
        = "void"
        | "char"
        | "short"
        | "int"
        | "long"
        | "float"
        | "double"
        | "signed"
        | "unsigned"
        | "_Bool"
        | "_Complex"
        | struct-or-union-specifier
        | enum-specifier
        | typedef-name;

struct-or-union-specifier
        = struct-or-union, [ identifier ], "{", { struct-declaration }, "}"
        | struct-or-union, identifier;

struct-or-union
        = "struct"
        | "union";

struct-declaration
        = specifier-qualifier-list, struct-declarator, { ",", struct-declarator }, ";";

specifier-qualifier-list
        = type-specifier, [ specifier-qualifier-list ]
        | type-qualifier, [ specifier-qualifier-list ];

struct-declarator
        = declarator
        | [ declarator ], ":", constant-expression;

enum-specifier
        = "enum", [ identifier ], "{", enumerator, { ",", enumerator }, [ "," ], "}"
        | "enum", identifier;

enumerator
        = identifier, [ "=", constant-expression ];

type-qualifier
        = "const"
        | "restrict"
        | "volatile";

function-specifier
        = "inline";

declarator
        = [ pointer ], direct-declarator;

direct-declarator
        = identifier
        | "(", declarator, ")"
        | direct-declarator, "[", { type-qualifier }, [ assignment-expression ], "]"
        | direct-declarator, "[", "static", type-qualifier, { type-qualifier }, assignment-expression, "]"
        | direct-declarator, "[", { type-qualifier }, "*", "]"
        | direct-declarator, "(", parameter-type-list, ")", constant
        | direct-declarator, "(", [ identifier, { ",", identifier } ], ")";

pointer
        = { "*", { type-qualifier } };

parameter-type-list
        = parameter-declaration, { ",", parameter-declaration }, [ ",", "..." ];

parameter-declaration
        = declaration-specifiers, ( declarator
        | [ abstract-declarator ] );

type-name
        = specifier-qualifier-list, [ abstract-declarator ];

abstract-declarator
        = pointer
        | [ pointer ], direct-abstract-declarator;

direct-abstract-declarator
        = "(", abstract-declarator, ")"
        | [ direct-abstract-declarator ], ( "[", ( { type-qualifier }, [ assignment-expression ]
        | "static", { type-qualifier }, assignment-expression
        | type-qualifier, { type-qualifier }, "static", assignment-expression
        | "*" ), "]"
        | "(", [ parameter-type-list ], ")" );

typedef-name
        = identifier;

initializer
        = assignment-expression
        | "{", initializer-list, [ "," ], "}";

initializer-list
        = [ designation ], initializer
        | initializer-list, ",", [ designation ], initializer;

designation
        = designator, { designator }, "=";

designator
        = "[", constant-expression, "]"
        | ".", identifier;

(* EXPRESSIONS *)
constant-expression
        = conditional-expression;

expression
        = assignment-expression, { ",", assignment-expression };

assignment-expression
        = conditional-expression
        | unary-expression, assignment-operator, assignment-expression;

assignment-operator
        = "="
        | "*="
        | "/="
        | "%="
        | "+="
        | "-="
        | "<<="
        | ">>="
        | "&="
        | "^="
        | "|=";

conditional-expression
        = logical-OR-expression, [ "?", expression, ":", expression ];

logical-OR-expression
        = logical-AND-expression, { "||", logical-AND-expression };

logical-AND-expression
        = inclusive-OR-expression, { "&&", inclusive-OR-expression };

inclusive-OR-expression
        = exclusive-OR-expression, { "|", exclusive-OR-expression };

exclusive-OR-expression
        = AND-expression, { "^", AND-expression };

AND-expression
        = equality-expression, { "&", equality-expression };

equality-expression
        = relational-expression, { ( "=="
        | "!=" ), relational-expression };

relational-expression
        = shift-expression, { ( "<"
        | ">"
        | "<="
        | ">=" ), shift-expression };

shift-expression
        = additive-expression, { ( "<<" | ">>" ), additive-expression };

additive-expression
        = multiplicative-expression, { ( "+"
        | "-" ), multiplicative-expression };

multiplicative-expression
        = cast-expression, { ( "*"
        | "/"
        | "%" ), cast-expression };

cast-expression
        = unary-expression
        | "(", type-name, ")", cast-expression;

unary-expression
        = postfix-expression
        | ( "++"
        | "--" ), unary-expression
        | unary-operator, cast-expression
        | "sizeof", unary-expression
        | "sizeof", "(", type-name, ")";

unary-operator
        = "&"
        | "*"
        | "+"
        | "-"
        | "~"
        | "!";

postfix-expression
        = primary-expression
        | postfix-expression, ( "[", expression, "]"
        | "(", { assignment-expression }, ")"
        | ".", identifier
        | "->", identifier
        | "++"
        | "--" )
        | "(", type-name, ")", "{", initializer-list, [ "," ], "}";

primary-expression
        = identifier
        | constant
        | string-literal
        | "(", expression, ")";